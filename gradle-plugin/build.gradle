repositories {
    gradlePluginPortal()
}

version = findProperty("gradle-plugin.version")

apply plugin: "java-gradle-plugin"
apply plugin: "maven-publish"
apply plugin: "com.gradle.plugin-publish"

gradlePlugin {
    plugins {
        fairy {
            id = "io.fairyproject"
            implementationClass = "io.fairyproject.gradle.FairyPlugin"
        }
    }
}

publishing {
    repositories {
        maven {
            name = 'localPluginRepository'
            url = '../libs/local'
        }
    }
}

pluginBundle {
    tags = ["fairy", "bukkit", "minecraft"]

    plugins {
        fairy {
            displayName = "Fairy Gradle Plugin"
            description = "The fairy gradle plugin"
            website = "https://github.com/FairyProject/fairy"
            vcsUrl = "https://github.com/FairyProject/fairy"
        }
    }
}

repositories {
    maven { url "https://repo.imanity.dev/imanity-libraries/" }
    maven { url "https://plugins.gradle.org/m2/" }
}

dependencies {
    compileOnly gradleApi()
    compileOnly 'org.codehaus.groovy:groovy-all:3.0.9'

    compileOnly libs.lombok
    annotationProcessor libs.lombok
    compileOnly libs.annotations
    annotationProcessor libs.annotations

    implementation libs.gson
    implementation libs.guava
    implementation libs.common.lang3
    implementation 'org.ow2.asm:asm:9.2'
    implementation 'org.ow2.asm:asm-commons:9.2'
    implementation 'gradle.plugin.com.github.johnrengelman:shadow:7.1.2'

    compileOnly project(":shared")

    testImplementation libs.junit
}

// Yeah this is hacky... but to prevent gradle from generating a problematic maven pom we have to shade it
def config = configurations.maybeCreate("shared")
config.dependencies.add(project.dependencies.create(project(":shared")))
List<Object> list = new ArrayList<>();
for (File file : config) {
    list.add(file.isDirectory() ? file : project.zipTree(file));
}

jar.from(list)

test {
    useJUnitPlatform()
}