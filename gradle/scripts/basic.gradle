//file:noinspection GroovyAssignabilityCheck

// Main script
//apply plugin: "io.freefair.aspectj.post-compile-weaving"

configurations {
    compileOnly {
        canBeResolved = true
    }
}

processResources {
    expand projectVersion: project.version
}
shadowJar.dependsOn build

tasks.withType(JavaCompile) {
    sourceCompatibility = targetCompatibility = "1.8"
    options.encoding = "UTF-8"
}

dependencies {
    compileOnly libs.lombok
    annotationProcessor libs.lombok
    testAnnotationProcessor libs.lombok
    compileOnly libs.annotations
    annotationProcessor libs.annotations

    compileOnly libs.bundles.jackson
    compileOnly libs.javax.persistence.api

    if (project.name.contains("-")) {
        def names = project.name.split("-")

        if (names[1] == "platform") {
            if (names[0] != "core") {
                api project(":io.fairyproject.platforms:core-platform")
            }
        } else {
            def platformName = names[0]

            compileOnly project(":io.fairyproject.platforms:$platformName-platform")
            testImplementation project(":io.fairyproject.tests:$platformName-tests")
        }
    } else {
        compileOnly project(":io.fairyproject.platforms:core-platform")
        testImplementation project(":io.fairyproject.tests:core-tests")
    }

    compileOnly libs.guava
    compileOnly libs.gson
    compileOnly libs.yaml
    compileOnly libs.common.lang3
    compileOnly libs.common.io
    compileOnly "net.md-5:bungeecord-chat:1.16-R0.3"

    // Test dependencies
    testImplementation libs.junit
}

sourceSets {
    test.compileClasspath += configurations.compileOnly
    test.runtimeClasspath += configurations.compileOnly
}