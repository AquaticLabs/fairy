apply plugin: "maven-publish"

project.afterEvaluate {
    def shadowJar = project.tasks.findByName("shadowJar")

    publishing {
        publications {
            maven(MavenPublication) { publication ->
                if (shadowJar != null)
                    from project.shadow.component(publication)
                else {
                    def java = components.findByName("java")
                    if (java != null)
                        from java
                }

                pom {

                    name = "Fairy " + project.name
                    description = project.description

                    url = "https://github.com/FairyProject/fairy"
                    organization {
                        name = "Fairy Project"
                        url = "https://github.com/FairyProject/fairy"
                    }
                    licenses {
                        license {
                            name = "MIT License"
                            url = "https://www.apache.org/licenses/LICENSE-2.0"
                            distribution = "repo"
                        }
                    }
                    scm {
                        url = "https://github.com/FairyProject/fairy"
                        connection = "scm:git:git://github.com/FairyProject/fairy"
                        developerConnection = "scm:git:git://github.com/FairyProject/fairy"
                    }
                    developers {
                        developer {
                            id = "leegod"
                            name = "LeeGod"
                            email = "leegod@imanity.dev"
                        }
                    }
                    issueManagement {
                        system = "GitHub"
                        url = "https://github.com/FairyProject/fairy/issues"
                    }
                }
            }
        }

        repositories {
            maven {
                name "Dev"
                url "$rootDir/libs/local"
            }

            maven {
                name "Production"
                url "https://repo.imanity.dev/imanity-libraries/"
                credentials {
                    username findProperty("imanityLibrariesUsername").toString()
                    password findProperty("imanityLibrariesPassword").toString()
                }
            }
        }
    }

    if (shadowJar != null) {
        tasks.withType(AbstractPublishToMaven).forEach(t -> {
            t.dependsOn(shadowJar)
        })
    }

    /* -------------------------------------------------------
     * Last edit: 03/03/2022
     * Fixes modules not properly being published with
     * the module.json
     * ------------------------------------------------------- */
    def moduleTask = project.tasks.findByName("module")
    if (moduleTask != null) {
        moduleTask.finalizedBy(publishAllPublicationsToDevRepository)
    } else if (shadowJar != null) {
        shadowJar.finalizedBy(publishAllPublicationsToDevRepository)
    } else {
        build.finalizedBy(publishAllPublicationsToDevRepository)
    }
}


